class Math {
	static Array twoToThe;
	static int n;

	function void init() {
		var int i;

		let n = 16;
		let twoToThe = Array.new(n);
		let twoToThe[0] = 1;
		let i = 1;

		while (i < n) {
			let twoToThe[i] = twoToThe[i-1] + twoToThe[i-1];
			let i = i + 1;
		}

		return;
	}

	function int twoToThe(int x) {
		return twoToThe[x];
	}

	function int mod(int x, int y) {
		return x - (y * (x / y));
	}

	function boolean bit(int x, int i) {
		var int p;
		let p = twoToThe[i];
		return (x & p) = p;
	}

	function int multiply(int x, int y) {
		var int sum, xShifted, i;

		let sum = 0;
		let xShifted = x;
		let i = 0;

		while (i<n) {
			if (Math.bit(y,i)) {
				let sum = sum + xShifted;
			}
			let xShifted = xShifted + xShifted;
			let i = i + 1;
		}

		return sum;
	}

	function int divide(int x, int y) {
		var int q, res;
		var boolean isPos;
		
		// if (y = 0) {do Sys.error(3);}
		
		let isPos = ((x < 0) = (y < 0));
		let x = Math.abs(x);
		let y = Math.abs(y);
		
		if (x < y) {return 0;}

		let q = Math.divide(x,y+y);

		if ((x - (2 * q * y)) < y) {let res = q + q;}
		else {let res = q + q + 1;}

		if (isPos) {return res;} else {return -res;}
	}

	function int sqrt(int x) {
		var int y, i, temp, temp2;

		// if (x < 0) {do Sys.error(4);}

		let i = 7;

		while (~(i < 0)) {
		    let temp = y + twoToThe[i];
			let temp2 = temp * temp;

			if (~(temp2 > x) & (temp2 > 0)){
				let y = temp;
			}

			let i = i - 1;
		}
		return y;
	}

	function int abs(int x) {
		if (x < 0) {
			return -x;
		}
		return x;
	}

	function int max(int x, int y) {
		if (x > y) {
			return x;
		}
		else {
			return y;
		}
	}

	function int min(int x, int y) {
		if (x > y) {
			return y;
		}
		else {
			return x;
		}
	}
}
